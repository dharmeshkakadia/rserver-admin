buildscript {
    repositories {
        maven { url "http://repo.spring.io/libs-snapshot" }
        maven { url 'http://repo.obiba.org/repo' }
        mavenLocal()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.0.0.RC1")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'application'

mainClassName = "org.obiba.rserver.Application"

jar {
    baseName = rootProject.name
}

tasks.startScripts {
    doLast {
        def scriptFile = file "${outputDir}/${applicationName}"
        scriptFile.text = scriptFile.text.replace('-classpath "$CLASSPATH"', '-Drserver.home="$APP_HOME" -classpath "$CLASSPATH"')
        scriptFile = file "${outputDir}/${applicationName}.bat"
        scriptFile.text = scriptFile.text.replace('-classpath "%CLASSPATH%"', '-Drserver.home="%APP_HOME%" -classpath "%CLASSPATH%"')
    }
}

tasks.distZip {
    archiveName = baseName + "-" + version + ".zip"
}

repositories {
    mavenCentral()
    maven { url "http://repo.spring.io/libs-snapshot" }
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web:1.0.0.RC1") {
        exclude module: "spring-boot-starter-tomcat"
    }
    compile("org.springframework.boot:spring-boot-starter-jetty:1.0.0.RC1")
    compile("com.google.guava:guava:15.0")
    compile("net.rforge:Rserve:0.6-8.1")
    compile("net.rforge:REngine:0.6-8.1")
    testCompile("junit:junit:4.11")
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.10'
}
